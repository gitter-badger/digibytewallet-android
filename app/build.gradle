buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: 'com.google.gms.google-services'

crashlytics {
    enableNdk = true
}

android {
    compileSdkVersion = 28

    dataBinding {
        enabled = true
    }

    defaultConfig {
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        applicationId = 'io.digibyte'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 2546
        versionName "3.0.1.7"
        multiDexEnabled true
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    flavorDimensions "mode"

    productFlavors {
        digibyte {
            applicationId "io.digibyte"
            dimension "mode"
            resValue "string", "app_name", "DigiByte"
            buildConfigField "boolean", "BITCOIN_TESTNET", "false"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DBITCOIN_TESTNET=0"
                    targets "core-lib"
                    arguments "-DANDROID_TOOLCHAIN=clang"
                }
            }
        }

        digibyteTestnet {
            applicationId "io.digibytetestnet"
            dimension "mode"
            resValue "string", "app_name", "digibyte-testnet"
            buildConfigField "boolean", "BITCOIN_TESTNET", "true"

            externalNativeBuild {
                cmake {
                    // When you specify a version of CMake, as shown below,
                    // the Android plugin searches for its binary within your
                    // PATH environmental variable.
                    cFlags "-DBITCOIN_TESTNET=1"
                    targets "core-lib"
                    arguments "-DANDROID_TOOLCHAIN=clang"
                }
            }

        }

    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        explainIssues true
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, only report errors
        ignoreWarnings false

        checkReleaseBuilds false

        disable 'MissingTranslation'
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled = true
            debuggable = false

            ndk {
                minifyEnabled = false
                debuggable = false
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable = true
            minifyEnabled = true

            ndk {
                minifyEnabled = false
                debuggable = true
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}

configurations {
    cleanedAnnotations
    compile.exclude group: 'org.jetbrains' , module:'annotations'
}

repositories {
    jcenter()
    mavenCentral()
    google()
    maven { url 'https://maven.google.com' }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'io.sigpipe:jbsdiff:1.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }
    implementation 'androidx.room:room-runtime:2.1.0'
    annotationProcessor 'androidx.room:room-compiler:2.1.0'
    implementation 'com.facebook.conceal:conceal:2.0.2@aar'
    implementation 'com.tarun0.zxing-standalone:zxing-standalone:1.0.0'
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.airbnb.android:lottie:2.5.4'
    implementation 'com.appolica:flubber:1.0.1'
    implementation 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
    implementation 'com.github.Binary-Finery:Bungee:1.0'
    implementation 'com.scottyab:rootbeer-lib:0.0.6'
    implementation group: 'com.google.guava', name: 'guava', version: '23.5-android'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation "com.squareup.okhttp3:okhttp-urlconnection:3.4.1"
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.10'
    implementation 'com.google.firebase:firebase-core:17.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.github.satyan:sugar:1.5'
    implementation "com.google.android.gms:play-services-gcm:17.0.0"
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.google.firebase:firebase-analytics:17.1.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'

}
